apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd")
}

def keystoreFilepath = ''
def keystorePSW = ''
def keystoreAlias = ''
def keystoreAliasPSW = ''
// default keystore file, PLZ config file path in local.properties
def keyfile = ''

Properties properties = new Properties()
// local.properties file in the root director
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyfile = file(keystoreFilepath)
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.martinboy.maskquery"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    // Add this block and enable/disable the parameters as follows
    bundle {
        density {
            // Different APKs are generated for devices with different screen densities; true by default.
            enableSplit true
        }
        abi {
            // Different APKs are generated for devices with different CPU architectures; true by default.
            enableSplit true
        }
        language {
            // This is disabled so that the App Bundle does NOT split the APK for each language.
            // We're gonna use the same APK for all languages.
            enableSplit false
        }
    }

    signingConfigs {
        release {
            storeFile keyfile
            keyAlias keystoreAlias
            storePassword keystorePSW
            keyPassword keystoreAliasPSW
            v2SigningEnabled false
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    flavorDimensions "version"
    productFlavors {
//        staging {
//            // Assigns this product flavor to the "version" flavor dimension.
//            // If you are using only one dimension, this property is optional,
//            // and the plugin automatically assigns all the module's flavors to
//            // that dimension.
//            dimension "version"
//            applicationIdSuffix ".staging"
//            versionNameSuffix "-staging"
//        }
        prod {
            dimension "version"
            applicationIdSuffix ".prpd"
            versionNameSuffix "-prod"
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "MaskQuery_${defaultConfig.versionCode}-${defaultConfig.versionName}-${releaseTime()}.apk"
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "android.arch.persistence.room:runtime:1.1.1"
    implementation "com.mcxiaoke.volley:library:1.0.19"
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.1'
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
